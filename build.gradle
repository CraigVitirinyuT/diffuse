import org.gradle.internal.jvm.Jvm

buildscript {
  repositories {
    mavenCentral()
    jcenter()
    gradlePluginPortal()
  }
  dependencies {
    classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.21'
    classpath 'org.jetbrains.dokka:dokka-gradle-plugin:0.9.17'
    classpath 'gradle.plugin.org.jlleitschuh.gradle:ktlint-gradle:5.0.0'
  }
}

apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'org.jlleitschuh.gradle.ktlint'

sourceCompatibility = JavaVersion.VERSION_1_6
targetCompatibility = JavaVersion.VERSION_1_6

version = '3.2.2-SNAPSHOT'

repositories {
  mavenCentral()
  google()
  jcenter()

  // TODO remove https://youtrack.jetbrains.com/issue/KT-27991
  maven { url "https://kotlin.bintray.com/kotlinx" }
}

configurations {
  r8
}

dependencies {
  def r8Version = 'com.android.tools:r8:1.3.52'

  implementation 'com.github.ajalt:clikt:1.5.0'
  implementation 'com.jakewharton.android.repackaged:dalvik-dx:9.0.0_r3'
  implementation r8Version
  implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.21'
  testImplementation 'junit:junit:4.12'
  testImplementation 'com.google.guava:guava:26.0-jre'
  testImplementation 'com.google.truth:truth:0.42'
  r8 r8Version
}

compileKotlin {
  kotlinOptions {
    jvmTarget = "1.8"
  }
}

dokka {
  outputFormat 'javadoc'
  outputDirectory "$buildDir/javadoc"
}

task javadocJar(type: Jar, dependsOn: 'dokka') {
  classifier 'javadoc'
  from "$buildDir/javadoc"
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task fatMembersJar(type: Jar, dependsOn: 'compileJava') {
  from files(sourceSets.main.output.classesDirs)
  from configurations.runtimeClasspath.asFileTree.files.collect { zipTree(it) }

  classifier 'fat-members'

  manifest {
   attributes 'Main-Class': 'com.jakewharton.dex.DexMembers'
  }
}

def dietMembersFile = new File(buildDir, "libs/dex-member-list-shrunk.jar")
def dietMembersJar = tasks.create('dietMembersJar', JavaExec) {
  classpath configurations.r8

  main = 'com.android.tools.r8.R8'
  args = [
      '--lib', new File(Jvm.current().jre.homeDir, "lib/rt.jar").getAbsolutePath(), // TODO restricts to JDK 8
      '--release',
      '--classfile',
      '--output', dietMembersFile.getAbsolutePath(),
      '--pg-conf', file('r8-rules.txt').getAbsolutePath(),
      fatMembersJar.archivePath
  ]

  dependsOn(fatMembersJar)
}

def binaryMembersFile = new File(buildDir, "libs/dex-member-list-binary.jar")
task binaryMembersJar() {
  dependsOn(dietMembersJar)

  inputs.file(dietMembersFile)
  outputs.file(binaryMembersFile)

  doLast {
    binaryMembersFile.getParentFile().mkdirs()
    binaryMembersFile.delete()
    binaryMembersFile << "#!/bin/sh\n\nexec java -jar \$0 \"\$@\"\n\n"
    dietMembersFile.withInputStream { binaryMembersFile.append it }

    binaryMembersFile.setExecutable true, false
  }
}

task copyBinaryMembers(type: Copy, dependsOn: binaryMembersJar) {
  from(binaryMembersFile)
  into buildDir
  rename(".*", 'dex-member-list')
}
assemble.dependsOn(copyBinaryMembers)

task fatMethodsJar(type: Jar, dependsOn: 'compileJava') {
  from files(sourceSets.main.output.classesDirs)
  from configurations.runtimeClasspath.asFileTree.files.collect { zipTree(it) }

  classifier 'fat-methods'

  manifest {
   attributes 'Main-Class': 'com.jakewharton.dex.DexMethods'
  }
}

def dietMethodsFile = new File(buildDir, "libs/dex-method-list-shrunk.jar")
def dietMethodsJar = tasks.create('dietMethodsJar', JavaExec) {
  classpath configurations.r8

  main = 'com.android.tools.r8.R8'
  args = [
      '--lib', new File(Jvm.current().jre.homeDir, "lib/rt.jar").getAbsolutePath(), // TODO restricts to JDK 8
      '--release',
      '--classfile',
      '--output', dietMethodsFile.getAbsolutePath(),
      '--pg-conf', file('r8-rules.txt').getAbsolutePath(),
      fatMethodsJar.archivePath
  ]

  dependsOn(fatMethodsJar)
}

def binaryMethodsFile = new File(buildDir, "libs/dex-method-list-binary.jar")
task binaryMethodsJar() {
  dependsOn(dietMethodsJar)

  inputs.file(dietMethodsFile)
  outputs.file(binaryMethodsFile)

  doLast {
    binaryMethodsFile.getParentFile().mkdirs()
    binaryMethodsFile.delete()
    binaryMethodsFile << "#!/bin/sh\n\nexec java -jar \$0 \"\$@\"\n\n"
    dietMethodsFile.withInputStream { binaryMethodsFile.append it }

    binaryMethodsFile.setExecutable true, false
  }
}

task copyBinaryMethods(type: Copy, dependsOn: binaryMethodsJar) {
  from(binaryMethodsFile)
  into buildDir
  rename(".*", 'dex-method-list')
}
assemble.dependsOn(copyBinaryMethods)


task fatFieldsJar(type: Jar, dependsOn: 'compileJava') {
  from files(sourceSets.main.output.classesDirs)
  from configurations.runtimeClasspath.asFileTree.files.collect { zipTree(it) }

  classifier 'fat-fields'

  manifest {
   attributes 'Main-Class': 'com.jakewharton.dex.DexFields'
  }
}

def dietFieldsFile = new File(buildDir, "libs/dex-field-list-shrunk.jar")
def dietFieldsJar = tasks.create('dietFieldsJar', JavaExec) {
  classpath configurations.r8

  main = 'com.android.tools.r8.R8'
  args = [
      '--lib', new File(Jvm.current().jre.homeDir, "lib/rt.jar").getAbsolutePath(), // TODO restricts to JDK 8
      '--release',
      '--classfile',
      '--output', dietFieldsFile.getAbsolutePath(),
      '--pg-conf', file('r8-rules.txt').getAbsolutePath(),
      fatFieldsJar.archivePath
  ]

  dependsOn(fatFieldsJar)
}

def binaryFieldsFile = new File(buildDir, "libs/dex-field-list-binary.jar")
task binaryFieldsJar() {
  dependsOn(dietFieldsJar)

  inputs.file(dietFieldsFile)
  outputs.file(binaryFieldsFile)

  doLast {
    binaryFieldsFile.getParentFile().mkdirs()
    binaryFieldsFile.delete()
    binaryFieldsFile << "#!/bin/sh\n\nexec java -jar \$0 \"\$@\"\n\n"
    dietFieldsFile.withInputStream { binaryFieldsFile.append it }

    binaryFieldsFile.setExecutable true, false
  }
}

task copyFieldsMethods(type: Copy, dependsOn: binaryFieldsJar) {
  from(binaryFieldsFile)
  into buildDir
  rename(".*", 'dex-field-list')
}
assemble.dependsOn(copyFieldsMethods)

artifacts {
  archives sourcesJar
  archives javadocJar
  archives file: binaryMethodsFile, name: 'binary', type: 'jar', builtBy: binaryMethodsJar, classifier: 'binary-methods'
  archives file: binaryFieldsFile, name: 'binary', type: 'jar', builtBy: binaryFieldsJar, classifier: 'binary-fields'
  archives file: binaryMembersFile, name: 'binary', type: 'jar', builtBy: binaryMembersJar, classifier: 'binary-members'
}

def isReleaseBuild() {
  return version.contains("SNAPSHOT") == false
}

def getRepositoryUsername() {
  return hasProperty('SONATYPE_NEXUS_USERNAME') ? SONATYPE_NEXUS_USERNAME : ""
}

def getRepositoryPassword() {
  return hasProperty('SONATYPE_NEXUS_PASSWORD') ? SONATYPE_NEXUS_PASSWORD : ""
}

signing {
  required { isReleaseBuild() && gradle.taskGraph.hasTask('uploadArchives') }
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
        authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
      }
      snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
        authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
      }

      pom.groupId = 'com.jakewharton.dex'
      pom.artifactId = 'dex-member-list'

      pom.project {
        name 'Dex Member List'
        packaging 'jar'
        description 'A utility which lists all method and/or field references in a dex file.'
        url 'https://github.com/JakeWharton/dex-member-list'

        scm {
          connection 'git@github.com:JakeWharton/dex-member-list.git'
          url 'https://github.com/JakeWharton/dex-member-list'
        }

        licenses {
          license {
            name 'Apache 2.0'
            url 'http://opensource.org/licenses/Apache-2.0'
          }
        }

        developers {
          developer {
            id 'jakewharton'
            name 'Jake Wharton'
          }
        }
      }
    }
  }
}
