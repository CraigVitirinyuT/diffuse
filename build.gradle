buildscript {
  ext.versions = [
      'kotlin': '1.3.50'
  ]

  ext.deps = [
      'androidTools': [
          'dalvikDx': 'com.jakewharton.android.repackaged:dalvik-dx:9.0.0_r3',
          'binaryResources': 'com.android.tools.apkparser:binary-resources:26.5.0',
          'apkSigner': 'com.android.tools.build:apksig:3.5.0'
      ],
      'kotlin': [
          'stdlibJdk8': "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}",
      ],
      'r8': 'com.android.tools:r8:1.5.68',
      'clikt': 'com.github.ajalt:clikt:2.2.0',
      'junit': 'junit:junit:4.13-beta-3',
      'truth': 'com.google.truth:truth:1.0',
      'guava': 'com.google.guava:guava:28.1-jre',
      'okio': 'com.squareup.okio:okio:2.4.1',
      'byteunits': 'com.jakewharton.byteunits:byteunits:0.9.1',
      'asm': 'org.ow2.asm:asm:7.2',
  ]

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    classpath 'org.jetbrains.dokka:dokka-gradle-plugin:0.9.17'
    classpath 'org.jlleitschuh.gradle:ktlint-gradle:9.0.0'
    classpath 'gradle.plugin.com.palantir.graal:gradle-graal:0.6.0-25-g2b2309b'
  }

  repositories {
    mavenCentral()
    jcenter()
    gradlePluginPortal()
  }
}

subprojects {
  group = GROUP
  version = VERSION_NAME

  repositories {
    mavenCentral()
    google()
    jcenter()
  }

  apply plugin: 'org.jlleitschuh.gradle.ktlint'
  ktlint {
    version = '0.35.0'
    disabledRules = [
        'filename',
    ]
  }
  afterEvaluate {
    tasks.named('check').configure {
      it.dependsOn(tasks.getByName('ktlintCheck'))
    }
  }

  plugins.withId('org.jetbrains.kotlin.jvm') {
    compileKotlin {
      kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = [
            "-XXLanguage:+InlineClasses",
            "-progressive",
        ]
      }
    }
  }
}
